Code
Code = DeclFunc Code | EntryPoint

EntryPoint = 'entryPoint' DeclFunc
DeclFunc = 'function' 'id' 'opParen' 'clParen' 'isType' Type Body
DeclFunc = 'function' 'id' 'opParen' FuncDeclVar 'clParen' 'isType' Type Body
DeclFunc = 'function' 'id' 'opParen' 'clParen' Body
DeclFunc = 'function' 'id' 'opParen' FuncDeclVar 'clParen' Body

FuncDeclVar = IdsList 'isType' Type 'comma' FuncDeclVar | IdsList 'isType' Type
DeclVars = IdsList 'isType' Type
ParamList = Expr 'comma' ParamList | Expr

IdsList = 'id' 'comma' IdsList | 'id'

Literal = 'bool' | 'int' | 'real' | 'char' | 'string'
Value = Literal | FuncCall | Array | 'id'

Type = 'typeBool' | 'typeInt' | 'typeReal' | 'typeChar' | 'typeString' | TypeArray
TypeArray = 'typeArray' 'opBrackets' 'clBrackets' 'of' Type
TypeArray = 'typeArray' 'opBrackets' Expr 'clBrackets' 'of' Type
Array = 'id' ArrayPos
ArrayPos = 'opBrackets' Expr 'clBrackets' ArrayPos | 'opBrackets' Expr 'clBrackets'

Body = 'opBraces' Lines 'clBraces' | 'opBraces' 'clBraces'

Lines = Line Lines | Line | 'returnFun' Expr | 'returnFun'
Line = Statement 'comma' Line | Statement
Statement = DeclVars | FuncCall | If | Loop | Attrib

Attrib = LeftAttrib 'attrib' Expr
LeftAttrib = 'id' | Array

If = 'ifSel' 'opParen' Expr 'clParen' IfBody
IfBody = Body ElIf | Body
ElIf = 'elseSel' Body | 'elseSel' If | 'elifSel' 'opParen' Expr 'clParen' IfBody

Expr = Expr 'logicOr' ExprAnd | ExprAnd
ExprAnd = ExprAnd 'logicAnd' ExprBor | ExprBor
ExprBor = ExprBor 'bitOr' ExprBxor | ExprBxor
ExprBxor = ExprBxor 'bitXor' ExprBand | ExprBand
ExprBand = ExprBand 'bitAnd' ExprEq | ExprEq
ExprEq = ExprEq 'eqOrDiff' ExprRel | ExprRel
ExprRel = ExprRel 'relational' ExprShift | ExprShift
ExprShift = ExprShift 'bitShift' ExprAdd | ExprAdd
ExprAdd = ExprAdd 'plus' ExprMult | ExprAdd 'minus' ExprMult | ExprMult
ExprMult = ExprMult 'mult' ExprPow | ExprPow
ExprPow = ExprPow 'exp' ExprUnary | ExprUnary
ExprUnary = 'minus' ExprCast | 'unary' ExprCast | ExprCast
ExprCast = ExprFac 'asCast' Type | ExprFac
ExprFac = Value | 'opParen' Expr 'clParen'

FuncCall = 'id' 'opParen' ParamList 'clParen' | 'id' 'opParen' 'clParen'
Loop = 'repeat' 'whileLoop' 'opParen' Expr 'clParen' Body
Loop = 'repeat' Body 'whileLoop' 'opParen' Expr 'clParen'
Loop = 'repeat' 'opParen' 'id' 'attrib' Expr 'to' Expr 'clParen' Body
Loop = 'repeat' 'opParen' 'id' 'attrib' Expr 'to' Expr 'at' Expr 'clParen' Body
